LIST P=16F877A
    INCLUDE "P16F877A.INC"

    __CONFIG _CP_OFF & _WDT_OFF & _BODEN_ON & _PWRTE_ON & _XT_OSC & _WRT_OFF & _LVP_OFF & _CPD_OFF

#define RS PORTB, 4
#define EN PORTB, 5
#define SW_MAN PORTA, 2

    CBLOCK 0x20
        SAYAC1, SAYAC2
        DESIRED_ST
        CURRENT_ST
        BIN_H, BIN_L, YUZ_H, ON_H, BIR_H
        LDR_VAL
        POT_VAL
        TEMP_MATH
        LAST_DISP_ST
        LAST_LDR
        PHASE_STEP
        SYSTEM_MODE
        UART_TEMP
        UART_LOW_BYTE
    ENDC

    ORG 0x00
    GOTO SETUP

SETUP
    BSF     STATUS, RP0
    CLRF    TRISD
    MOVLW   B'11000000'
    MOVWF   TRISB
    MOVLW   B'00000111'
    MOVWF   TRISA
    BCF     TRISC, 6
    BSF     TRISC, 7
    MOVLW   D'25'
    MOVWF   SPBRG
    MOVLW   B'00100100'
    MOVWF   TXSTA
    MOVLW   B'00000100'
    MOVWF   ADCON1
    BCF     STATUS, RP0
    MOVLW   B'10010000'
    MOVWF   RCSTA
    MOVLW   B'10000001'
    MOVWF   ADCON0
    CLRF    SYSTEM_MODE
    CLRF    PHASE_STEP
    CLRF    UART_LOW_BYTE
    MOVLW   D'32'
    MOVWF   DESIRED_ST
    MOVWF   CURRENT_ST
    MOVLW   0xFF
    MOVWF   LAST_DISP_ST
    CALL    LCD_INIT
    CALL    EKRAN_SABLON
    GOTO    ANA_DONGU

ANA_DONGU
    CALL    UART_DINLE_VE_ISLE
    CALL    SENSORLERI_OKU
    MOVF    SYSTEM_MODE, W
    SUBLW   D'2'
    BTFSC   STATUS, Z
    GOTO    ISLEM_PC
    BTFSC   SW_MAN
    GOTO    ISLEM_MANUEL
    GOTO    ISLEM_AUTO

ISLEM_MANUEL
    MOVLW   D'1'
    MOVWF   SYSTEM_MODE
    MOVF    POT_VAL, W
    MOVWF   TEMP_MATH
    BCF     STATUS, C
    RRF     TEMP_MATH, F
    BCF     STATUS, C
    RRF     TEMP_MATH, W
    MOVWF   DESIRED_ST
    CALL    LIMIT_63
    GOTO    UYGULA

ISLEM_AUTO
    MOVLW   D'0'
    MOVWF   SYSTEM_MODE
    MOVLW   D'100'
    SUBWF   LDR_VAL, W
    BTFSS   STATUS, C
    GOTO    AUTO_KARANLIK
    CLRF    DESIRED_ST
    GOTO    UYGULA
AUTO_KARANLIK
    MOVLW   D'63'
    MOVWF   DESIRED_ST

UYGULA
    CALL    MOTOR_HAREKET
    CALL    EKRANI_GUNCELLE
    GOTO    ANA_DONGU

ISLEM_PC
    CALL    MOTOR_HAREKET
    CALL    EKRANI_GUNCELLE
    GOTO    ANA_DONGU

LIMIT_63
    MOVLW   D'63'
    SUBWF   DESIRED_ST, W
    BTFSC   STATUS, C
    GOTO    SET_MAX_63
    RETURN
SET_MAX_63
    MOVLW   D'63'
    MOVWF   DESIRED_ST
    RETURN

SENSORLERI_OKU
    MOVLW   B'10000001'
    MOVWF   ADCON0
    CALL    GECIKME_ADC_SETUP
    BSF     ADCON0, GO
WT_LDR
    BTFSC   ADCON0, GO
    GOTO    WT_LDR
    MOVF    ADRESH, W
    MOVWF   TEMP_MATH
    MOVF    LDR_VAL, W
    SUBWF   TEMP_MATH, W
    BTFSC   STATUS, Z
    GOTO    LDR_SKIP
    MOVF    TEMP_MATH, W
    MOVWF   LDR_VAL
LDR_SKIP
    MOVLW   B'10001001'
    MOVWF   ADCON0
    CALL    GECIKME_ADC_SETUP
    BSF     ADCON0, GO
WT_POT
    BTFSC   ADCON0, GO
    GOTO    WT_POT
    MOVF    ADRESH, W
    MOVWF   POT_VAL
    RETURN

MOTOR_HAREKET
    MOVF    CURRENT_ST, W
    SUBWF   DESIRED_ST, W
    BTFSC   STATUS, Z
    RETURN
    BTFSC   STATUS, C
    GOTO    MOVE_CW
    GOTO    MOVE_CCW

MOVE_CW
    CALL    STEP_CW
    INCF    CURRENT_ST, F
    RETURN
MOVE_CCW
    CALL    STEP_CCW
    DECF    CURRENT_ST, F
    RETURN

STEP_CW
    INCF    PHASE_STEP, F
    GOTO    DRIVE
STEP_CCW
    DECF    PHASE_STEP, F
    GOTO    DRIVE

DRIVE
    MOVF    PHASE_STEP, W
    ANDLW   0x03
    CALL    STEP_TABLE
    MOVWF   TEMP_MATH
    MOVF    PORTB, W
    ANDLW   B'11110000'
    IORWF   TEMP_MATH, W
    MOVWF   PORTB
    CALL    GECIKME_MOTOR
    RETURN

STEP_TABLE
    ADDWF   PCL, F
    RETLW   B'00000001'
    RETLW   B'00000010'
    RETLW   B'00000100'
    RETLW   B'00001000'

UART_DINLE_VE_ISLE
    BANKSEL PIR1
    BTFSS   PIR1, RCIF
    RETURN
    BANKSEL RCSTA
    BTFSC   RCSTA, OERR
    GOTO    UART_ERR
    BANKSEL RCREG
    MOVF    RCREG, W
    MOVWF   UART_TEMP
    BANKSEL 0
    MOVLW   0x01
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_DESIRED_LOW
    MOVLW   0x02
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_DESIRED_HIGH
    MOVLW   0x03
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_TEMP_LOW
    MOVLW   0x04
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_TEMP_HIGH
    MOVLW   0x05
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_PRESS_LOW
    MOVLW   0x06
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_PRESS_HIGH
    MOVLW   0x07
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_LIGHT_LOW
    MOVLW   0x08
    SUBWF   UART_TEMP, W
    BTFSC   STATUS, Z
    GOTO    TX_LIGHT_HIGH
    MOVF    UART_TEMP, W
    ANDLW   B'11000000'
    XORLW   B'10000000'
    BTFSC   STATUS, Z
    GOTO    UART_SET_LOW
    MOVF    UART_TEMP, W
    ANDLW   B'11000000'
    XORLW   B'11000000'
    BTFSC   STATUS, Z
    GOTO    UART_SET_HIGH
    RETURN

TX_DESIRED_LOW
    MOVLW   D'0'
    GOTO    TX_W

TX_DESIRED_HIGH
    MOVF    CURRENT_ST, W
    GOTO    TX_W

TX_TEMP_LOW
    MOVLW   D'0'
    GOTO    TX_W

TX_TEMP_HIGH
    MOVLW   D'20'
    GOTO    TX_W

TX_PRESS_LOW
    MOVLW   D'3'
    GOTO    TX_W

TX_PRESS_HIGH
    MOVLW   D'101'
    GOTO    TX_W

TX_LIGHT_LOW
    MOVLW   D'0'
    GOTO    TX_W

TX_LIGHT_HIGH
    MOVF    LDR_VAL, W
    GOTO    TX_W

TX_W
    BANKSEL PIR1
WT_TX
    BTFSS   PIR1, TXIF
    GOTO    WT_TX
    BANKSEL TXREG
    MOVWF   TXREG
    BANKSEL 0
    RETURN

UART_SET_LOW
    MOVF    UART_TEMP, W
    ANDLW   B'00111111'
    MOVWF   UART_LOW_BYTE
    RETURN

UART_SET_HIGH
    MOVLW   D'2'
    MOVWF   SYSTEM_MODE
    MOVF    UART_TEMP, W
    ANDLW   B'00111111'
    MOVWF   DESIRED_ST
    RETURN

UART_ERR
    BCF     RCSTA, CREN
    BSF     RCSTA, CREN
    RETURN

EKRANI_GUNCELLE
    MOVF    CURRENT_ST, W
    XORWF   LAST_DISP_ST, W
    BTFSS   STATUS, Z
    GOTO    YAZDIR
    MOVF    LDR_VAL, W
    XORWF   LAST_LDR, W
    BTFSS   STATUS, Z
    GOTO    YAZDIR
    RETURN

YAZDIR
    MOVF    CURRENT_ST, W
    MOVWF   LAST_DISP_ST
    MOVF    LDR_VAL, W
    MOVWF   LAST_LDR
    MOVLW   0x86
    CALL    LCD_KOMUT
    MOVF    SYSTEM_MODE, W
    SUBLW   D'2'
    BTFSC   STATUS, Z
    GOTO    W_PC
    BTFSC   SW_MAN
    GOTO    W_MAN
    GOTO    W_AUTO

W_PC
    MOVLW   'P'
    CALL    LCD_VERI
    MOVLW   'C'
    CALL    LCD_VERI
    GOTO    W_VALS

W_MAN
    MOVLW   'M'
    CALL    LCD_VERI
    MOVLW   'A'
    CALL    LCD_VERI
    GOTO    W_VALS

W_AUTO
    MOVLW   'O'
    CALL    LCD_VERI
    MOVLW   'T'
    CALL    LCD_VERI

W_VALS
    MOVLW   0xC2
    CALL    LCD_KOMUT
    MOVF    LDR_VAL, W
    CALL    SAYI_0_255_TO_PERCENT
    CALL    SAYI_BAS
    MOVLW   0xCB
    CALL    LCD_KOMUT
    MOVF    CURRENT_ST, W
    CALL    SAYI_0_63_TO_PERCENT
    CALL    SAYI_BAS
    RETURN

SAYI_0_255_TO_PERCENT
    MOVWF   BIN_L
    RETURN

SAYI_0_63_TO_PERCENT
    MOVWF   BIN_L
    MOVWF   TEMP_MATH
    BCF     STATUS, C
    RRF     TEMP_MATH, F
    MOVF    TEMP_MATH, W
    ADDWF   BIN_L, F
    BCF     STATUS, C
    RRF     TEMP_MATH, F
    BCF     STATUS, C
    RRF     TEMP_MATH, F
    BCF     STATUS, C
    RRF     TEMP_MATH, W
    ADDWF   BIN_L, W
    MOVWF   BIN_L
    RETURN

SAYI_BAS
    MOVF    BIN_L, W
    CLRF    YUZ_H
    CLRF    ON_H
    CLRF    BIR_H

L_Y
    MOVLW   D'100'
    SUBWF   BIN_L, W
    BTFSS   STATUS, C
    GOTO    L_O
    MOVWF   BIN_L
    INCF    YUZ_H, F
    GOTO    L_Y

L_O
    MOVLW   D'10'
    SUBWF   BIN_L, W
    BTFSS   STATUS, C
    GOTO    L_B
    MOVWF   BIN_L
    INCF    ON_H, F
    GOTO    L_O

L_B
    MOVF    BIN_L, W
    MOVWF   BIR_H
    MOVF    YUZ_H, W
    ADDLW   0x30
    CALL    LCD_VERI
    MOVF    ON_H, W
    ADDLW   0x30
    CALL    LCD_VERI
    MOVF    BIR_H, W
    ADDLW   0x30
    CALL    LCD_VERI
    RETURN

GECIKME_MOTOR
    MOVLW   D'200'
    MOVWF   SAYAC2
G_M_OUTER
    MOVLW   D'200'
    MOVWF   SAYAC1
G_M_INNER
    DECFSZ  SAYAC1, F
    GOTO    G_M_INNER
    DECFSZ  SAYAC2, F
    GOTO    G_M_OUTER
    RETURN

GECIKME_ADC_SETUP
    MOVLW   D'20'
    MOVWF   SAYAC1
G_ADC
    DECFSZ  SAYAC1, F
    GOTO    G_ADC
    RETURN

LCD_INIT
    CALL    GECIKME_MOTOR
    MOVLW   0x38
    CALL    LCD_KOMUT
    MOVLW   0x0C
    CALL    LCD_KOMUT
    MOVLW   0x01
    CALL    LCD_KOMUT
    CALL    GECIKME_MOTOR
    RETURN

LCD_KOMUT
    MOVWF   PORTD
    BCF     RS
    BSF     EN
    NOP
    BCF     EN
    CALL    G_LCD
    RETURN

LCD_VERI
    MOVWF   PORTD
    BSF     RS
    BSF     EN
    NOP
    BCF     EN
    CALL    G_LCD
    RETURN

G_LCD
    MOVLW   D'100'
    MOVWF   SAYAC1
G_LCD_LOOP
    DECFSZ  SAYAC1, F
    GOTO    G_LCD_LOOP
    RETURN

EKRAN_SABLON
    MOVLW   0x80
    CALL    LCD_KOMUT
    MOVLW   'M'
    CALL    LCD_VERI
    MOVLW   'O'
    CALL    LCD_VERI
    MOVLW   'D'
    CALL    LCD_VERI
    MOVLW   ':'
    CALL    LCD_VERI
    MOVLW   0xC0
    CALL    LCD_KOMUT
    MOVLW   'L'
    CALL    LCD_VERI
    MOVLW   ':'
    CALL    LCD_VERI
    MOVLW   0xC9
    CALL    LCD_KOMUT
    MOVLW   'C'
    CALL    LCD_VERI
    MOVLW   ':'
    CALL    LCD_VERI
    RETURN

    END